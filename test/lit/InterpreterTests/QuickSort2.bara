// RUN: bara %s -interpret | FileCheck %s

fn partition(list, start, end) {
  var pivot = list[start];

  var i = start + 1;
  for (var j = i; j <= end; j += 1) {
    if list[j] < pivot {
      (list[i], list[j]) = (list[j], list[i]);
      i += 1;
    }
  }

  (list[start], list[i - 1]) = (list[i - 1], list[start]);
  return i - 1;
}

fn quickSortImpl(list, start, end) {
  if start < end {
    var pivotIndex = random(start, end + 1); 
    (list[start], list[pivotIndex]) = (list[pivotIndex], list[start]);
    pivotIndex = partition(list, start, end);
    quickSortImpl(list, start, pivotIndex - 1);
    quickSortImpl(list, pivotIndex + 1, end);
  }
}

fn quickSort(list) {
  quickSortImpl(list, 0, len(list) - 1);
}

var list = [12, 11, 13, 5, 6, 7];
quickSort(list);
print(list);
// CHECK: [5, 6, 7, 11, 12, 13]

var list2 = [(2, 1), (1, 2), (3, 1), (1, 3), (2, 3), (3, 2)];
quickSort(list2);
print(list2);
// CHECK: [(1, 2), (1, 3), (2, 1), (2, 3), (3, 1), (3, 2)]
